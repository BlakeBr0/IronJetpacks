buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.18.1-5.0.2'
group = 'com.blakebr0.ironjetpacks'
archivesBaseName = 'IronJetpacks'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

repositories {
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.18.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                ironjetpacks {
                    source sourceSets.main
                }

                cucumber {
                    source project(':Cucumber').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                ironjetpacks {
                    source sourceSets.main
                }

                cucumber {
                    source project(':Cucumber').sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.0.1'

    implementation project(':Cucumber')

    compileOnly fg.deobf('mezz.jei:jei-1.18.1:9.1.0.41:api')

    runtimeOnly fg.deobf('mezz.jei:jei-1.18.1:9.1.0.41')
    runtimeOnly fg.deobf('curse.maven:jade-324717:3544015')
}

jar {
    manifest {
        attributes([
                'Specification-Title': 'Iron Jetpacks',
                'Specification-Vendor': 'BlakeBr0',
                'Specification-Version': '1',
                'Implementation-Title': project.name,
                'Implementation-Version': "${version.split('-').last()}",
                'Implementation-Vendor' : 'BlakeBr0',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}